apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'bugly'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    def appName = "zhihu";
    defaultConfig {
        applicationId 'com.bill.zhihu'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 13
        versionName "0.2.7"
        renameApk(appName + "-" + versionName)

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    signingConfigs {
        release {
            storeFile file("../keystore/zhihu.keystore.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
        preview {
            storeFile file("../keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            shrinkResources true
            resValue "string", "app_version_name", "${defaultConfig.versionName}-release"
            resValue "string", "app_name", "知乎"
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_version_name", "${defaultConfig.versionName}-debug"
            resValue "string", "app_name", "知乎-debug"
        }
        preview {
            applicationIdSuffix ".preview"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.preview
            resValue "string", "app_version_name", "${defaultConfig.versionName}-preview"
            resValue "string", "app_name", "知乎-preview"
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['src/main/res', 'src/main/res/drawable-hdpi', 'src/main/res/drawable-xhdpi', 'src/main/res/drawable-xxhdpi']
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

bugly {
    appId = '900009454'
    appKey = 'SGbSptp3pKv7GUwK'
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:cardview-v7:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:palette-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.hannesdorfmann:swipeback:1.0.3'
    compile 'com.github.chrisbanes:PhotoView:1.2.6'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.karumi:expandableselector:1.0.0'
    compile 'hanks.xyz:htextview-library:0.1.3'

    compile 'com.tencent.bugly:crashreport_upgrade:1.0.1'

    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'

    compile 'com.tramsun.libs:prefcompat:0.8'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    debugCompile 'com.github.moduth:blockcanary-ui:1.2.0'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'
    releaseCompile 'com.github.moduth:blockcanary-no-op:1.2.0'
    previewCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'
    previewCompile 'com.github.moduth:blockcanary-no-op:1.2.0'

    compile project(':zhihuapi')
}

void renameApk(fileName) {
    setProperty("archivesBaseName", fileName)
}